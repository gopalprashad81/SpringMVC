buildscript {
    def springRepo = 'http://repo.spring.io'
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "$springRepo/release" }
        maven { url "$springRepo/snapshot" }
        maven { url "$springRepo/milestone" }
        maven { url "$springRepo/libs-milestone" }
    }

    dependencies {
        classpath boot.springBootPlugin
    }
}

apply plugin: 'java-library'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

dependencies {
    annotationProcessor boot.annotationProcessor
    implementation spring.r2dbcData, boot.starterWebflux, boot.actuator, misc.r2dbcH2
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    testImplementation testing.reactorTest
    testImplementation (boot.starterTest) {
        exclude group: "org.junit.vintage", module: "junit-vintage-engine"
        exclude group: "junit", module: "junit"
    }
}

dependencyManagement {
    imports {
        mavenBom misc.r2dbcBom
    }
}

test {
    useJUnitPlatform()

    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
}

// decomment this after leaving a single main class to build jar
jar {
    archiveBaseName = "reactive-boot-r2dbc"
    manifest {
        attributes(
                "Created-By": System.getProperty('java.version'),
                "Specification-Title": "Pivotal Certified Professional Spring Developer Exam - Chapter 12 - Spring Boot Webflux Application - not totally reactive - using router functions",
                "Implementation-Version": archiveVersion,
                "Main-Class"         : "com.apress.cems.reactive.ReactiveApplication",
                "Class-Path": configurations.compile.collect { it.getName() }.join(' ')
        )
    }
}

